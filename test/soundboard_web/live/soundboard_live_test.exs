defmodule SoundboardWeb.SoundboardLiveTest do
  @moduledoc false
  use SoundboardWeb.ConnCase
  import Phoenix.LiveViewTest
  alias Soundboard.{Accounts.User, Repo, Sound, Tag}
  import Mock

  setup %{conn: conn} do
    Repo.delete_all(Sound)
    Repo.delete_all(User)

    {:ok, user} =
      %User{}
      |> User.changeset(%{
        username: "testuser",
        discord_id: "123",
        avatar: "test.jpg"
      })
      |> Repo.insert()

    {:ok, sound} =
      %Sound{}
      |> Sound.changeset(%{
        filename: "test.mp3",
        source_type: "local",
        user_id: user.id
      })
      |> Repo.insert()

    conn = conn |> init_test_session(%{user_id: user.id})

    {:ok, conn: conn, user: user, sound: sound}
  end

  describe "Soundboard LiveView" do
    test "mounts successfully with user session", %{conn: conn} do
      {:ok, _, html} = live(conn, "/")

      assert html =~ "Soundboard"
      # Check for the main content instead of a specific container
      assert html =~ "SoundBored"
    end

    test "can search sounds", %{conn: conn} do
      {:ok, view, _html} = live(conn, "/")

      view
      |> element("form")
      |> render_change(%{"query" => "test"})

      rendered = render(view)
      assert rendered =~ "test.mp3"
    end

    test "can play sound", %{conn: conn, sound: sound} do
      {:ok, view, _html} = live(conn, "/")

      with_mock Soundboard.AudioPlayer, play_sound: fn _, _ -> :ok end do
        rendered =
          view
          |> element("[phx-click='play'][phx-value-name='#{sound.filename}']")
          |> render_click()

        assert rendered =~ sound.filename
      end
    end

    test "play random respects current search results", %{conn: conn, user: user} do
      %Sound{}
      |> Sound.changeset(%{
        filename: "filtered.mp3",
        source_type: "local",
        user_id: user.id
      })
      |> Repo.insert!()

      {:ok, view, _html} = live(conn, "/")

      view
      |> element("form")
      |> render_change(%{"query" => "filtered"})

      with_mock Soundboard.AudioPlayer, play_sound: fn _, _ -> :ok end do
        view
        |> element("[phx-click='play_random']")
        |> render_click()

        assert_called(Soundboard.AudioPlayer.play_sound("filtered.mp3", :_))
      end
    end

    test "play random respects selected tags", %{conn: conn, user: user} do
      tag =
        %Tag{}
        |> Tag.changeset(%{name: "funny"})
        |> Repo.insert!()

      %Sound{}
      |> Sound.changeset(%{
        filename: "funny.mp3",
        source_type: "local",
        user_id: user.id,
        tags: [tag]
      })
      |> Repo.insert!()

      {:ok, view, _html} = live(conn, "/")

      view
      |> element("div.hidden.sm\\:flex button[phx-value-tag='funny']")
      |> render_click()

      with_mock Soundboard.AudioPlayer, play_sound: fn _, _ -> :ok end do
        view
        |> element("[phx-click='play_random']")
        |> render_click()

        assert_called(Soundboard.AudioPlayer.play_sound("funny.mp3", :_))
      end
    end

    test "can open and close upload modal", %{conn: conn} do
      {:ok, view, _html} = live(conn, "/")

      # First verify we can see the Add Sound button
      assert render(view) =~ "Add Sound"

      # Click the Add Sound button and verify modal appears
      view
      |> element("[phx-click='show_upload_modal']")
      |> render_click()

      # The modal should be visible now, verify its presence using form ID and content
      assert has_element?(view, "#upload-form")
      assert has_element?(view, "form[phx-submit='save_upload']")
      assert has_element?(view, "select[name='source_type']")
      assert render(view) =~ "Source Type"

      # Close the modal using the correct phx-click value
      view
      |> element("[phx-click='close_upload_modal']")
      |> render_click()

      # Verify modal is gone by checking for the form
      refute has_element?(view, "#upload-form")
    end

    test "can edit sound", %{conn: conn, sound: sound} do
      {:ok, view, _html} = live(conn, "/")

      rendered =
        view
        |> element("[phx-click='edit'][phx-value-id='#{sound.id}']")
        |> render_click()

      assert rendered =~ "Edit Sound"

      params = %{
        "filename" => "updated",
        "source_type" => "local",
        "volume" => "80"
      }

      uploads_dir = uploads_dir()
      File.mkdir_p!(uploads_dir)

      test_file = Path.join(uploads_dir, "test.mp3")
      updated_file = Path.join(uploads_dir, "updated.mp3")

      unless File.exists?(test_file) do
        File.write!(test_file, "test content")
      end

      # Target the edit form specifically
      view
      |> element("#edit-form")
      |> render_submit(params)

      # Clean up both original and updated files
      File.rm_rf!(test_file)
      File.rm_rf!(updated_file)

      updated_sound = Repo.get(Sound, sound.id)
      assert updated_sound.filename == "updated.mp3"
      assert_in_delta updated_sound.volume, 0.8, 0.0001
    end

    test "shared sounds can be edited by any signed-in user but only deleted by the uploader", %{
      conn: conn
    } do
      {:ok, other_user} =
        %User{}
        |> User.changeset(%{
          username: "other_#{System.unique_integer([:positive])}",
          discord_id: Integer.to_string(System.unique_integer([:positive])),
          avatar: "other.jpg"
        })
        |> Repo.insert()

      {:ok, other_sound} =
        %Sound{}
        |> Sound.changeset(%{
          filename: "other-owned.mp3",
          source_type: "local",
          user_id: other_user.id
        })
        |> Repo.insert()

      {:ok, view, _html} = live(conn, "/")

      assert has_element?(view, "[phx-click='edit'][phx-value-id='#{other_sound.id}']")

      rendered =
        view
        |> element("[phx-click='edit'][phx-value-id='#{other_sound.id}']")
        |> render_click()

      assert rendered =~ "Edit Sound"
      refute rendered =~ "Delete Sound"
    end

    test "edit validation preserves the current sound extension when checking duplicates", %{
      conn: conn,
      user: user
    } do
      {:ok, current_sound} =
        %Sound{}
        |> Sound.changeset(%{
          filename: "current.wav",
          source_type: "local",
          user_id: user.id
        })
        |> Repo.insert()

      {:ok, _existing_sound} =
        %Sound{}
        |> Sound.changeset(%{
          filename: "taken.wav",
          source_type: "local",
          user_id: user.id
        })
        |> Repo.insert()

      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='edit'][phx-value-id='#{current_sound.id}']")
      |> render_click()

      view
      |> element("#edit-form")
      |> render_change(%{
        "_target" => ["filename"],
        "sound_id" => current_sound.id,
        "filename" => "taken"
      })

      assert render(view) =~ "A sound with that name already exists"
    end

    test "slider volume change persists on save", %{conn: conn, sound: sound} do
      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='edit'][phx-value-id='#{sound.id}']")
      |> render_click()

      render_hook(view, :update_volume, %{"volume" => 27, "target" => "edit"})

      base_filename = Path.rootname(sound.filename)

      view
      |> element("#edit-form")
      |> render_submit(%{
        "filename" => base_filename,
        "source_type" => sound.source_type,
        "volume" => "27"
      })

      updated_sound = Repo.get!(Sound, sound.id)
      assert_in_delta updated_sound.volume, 0.27, 0.0001
    end

    test "can delete sound", %{conn: conn, sound: sound} do
      {:ok, view, _html} = live(conn, "/")

      uploads_dir = uploads_dir()
      test_file = Path.join(uploads_dir, "test.mp3")
      File.mkdir_p!(uploads_dir)
      File.write!(test_file, "test content")

      view
      |> element("[phx-click='edit'][phx-value-id='#{sound.id}']")
      |> render_click()

      view
      |> element("[phx-click='show_delete_confirm']")
      |> render_click()

      view
      |> element("[phx-click='delete_sound']")
      |> render_click()

      File.rm_rf!(test_file)

      assert Repo.get(Sound, sound.id) == nil
    end

    test "url upload allows setting url before name", %{conn: conn} do
      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='show_upload_modal']")
      |> render_click()

      view
      |> element("select[name='source_type']")
      |> render_change(%{"source_type" => "url"})

      html =
        view
        |> element("#upload-form")
        |> render_change(%{"url" => "https://example.com/beep.mp3"})

      refute html =~ "Please select a file"
      refute html =~ "can't be blank"
      assert html =~ "https://example.com/beep.mp3"
    end

    test "can upload sound from url", %{conn: conn, user: user} do
      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='show_upload_modal']")
      |> render_click()

      view
      |> element("select[name='source_type']")
      |> render_change(%{"source_type" => "url"})

      params = %{
        "url" => "https://example.com/wow.mp3",
        "name" => "wow"
      }

      view
      |> element("#upload-form")
      |> render_submit(params)

      new_sound = Repo.get_by!(Sound, filename: "wow.mp3")
      assert new_sound.source_type == "url"
      assert new_sound.url == "https://example.com/wow.mp3"
      assert new_sound.user_id == user.id

      Repo.delete!(new_sound)
    end

    test "upload sound from url saves provided volume", %{conn: conn} do
      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='show_upload_modal']")
      |> render_click()

      view
      |> element("select[name='source_type']")
      |> render_change(%{"source_type" => "url"})

      view
      |> element("#upload-form")
      |> render_submit(%{
        "url" => "https://example.com/soft.mp3",
        "name" => "soft",
        "volume" => "25"
      })

      sound = Repo.get_by!(Sound, filename: "soft.mp3")
      assert_in_delta sound.volume, 0.25, 0.0001

      Repo.delete!(sound)
    end

    test "deleting a local sound removes the file", %{conn: conn, sound: sound} do
      {:ok, view, _html} = live(conn, "/")

      uploads_dir = uploads_dir()
      File.mkdir_p!(uploads_dir)
      sound_path = Path.join(uploads_dir, sound.filename)
      File.write!(sound_path, "test content")

      view
      |> element("[phx-click='edit'][phx-value-id='#{sound.id}']")
      |> render_click()

      view
      |> element("[phx-click='show_delete_confirm']")
      |> render_click()

      view
      |> element("[phx-click='delete_sound']")
      |> render_click()

      refute File.exists?(sound_path)
      assert Repo.get(Sound, sound.id) == nil
    end

    test "failed rename keeps original file", %{conn: conn, user: user, sound: sound} do
      {:ok, conflict_sound} =
        %Sound{}
        |> Sound.changeset(%{
          filename: "conflict.mp3",
          source_type: "local",
          user_id: user.id
        })
        |> Repo.insert()

      uploads_dir = uploads_dir()
      File.mkdir_p!(uploads_dir)

      original_path = Path.join(uploads_dir, sound.filename)
      conflict_path = Path.join(uploads_dir, conflict_sound.filename)

      File.write!(original_path, "original")
      File.rm_rf!(conflict_path)

      on_exit(fn ->
        uploads_dir = uploads_dir()
        File.rm_rf!(Path.join(uploads_dir, sound.filename))
        File.rm_rf!(Path.join(uploads_dir, "conflict.mp3"))
      end)

      {:ok, view, _html} = live(conn, "/")

      view
      |> element("[phx-click='edit'][phx-value-id='#{sound.id}']")
      |> render_click()

      _html =
        view
        |> element("#edit-form")
        |> render_submit(%{
          "filename" => "conflict",
          "source_type" => "local",
          "url" => "",
          "sound_id" => Integer.to_string(sound.id)
        })

      assert File.exists?(original_path)
      refute File.exists?(conflict_path)
      assert Repo.get!(Sound, sound.id).filename == sound.filename
    end

    test "handles pubsub updates", %{conn: conn} do
      {:ok, view, _html} = live(conn, "/")

      Soundboard.PubSubTopics.broadcast_files_updated()

      # Just verify the view is still alive
      assert render(view) =~ "SoundBored"
    end
  end

  defp uploads_dir do
    Soundboard.UploadsPath.dir()
  end

  describe "image upload" do
    alias Soundboard.Sounds.ImageProcessing

    @fixture_image "test/support/fixtures/test_image.png"

    test "image_filename is stored on sound after upload", %{conn: conn, user: _user} do
      {:ok, view, _html} = live(conn, "/")

      view |> element("[phx-click='show_upload_modal']") |> render_click()

      view
      |> element("select[name='source_type']")
      |> render_change(%{"source_type" => "url"})

      image_content = File.read!(@fixture_image)

      image_input =
        file_input(view, "#upload-form", :image, [
          %{name: "test_image.png", content: image_content, type: "image/png"}
        ])

      render_upload(image_input, "test_image.png")

      view
      |> element("#upload-form")
      |> render_submit(%{
        "name" => "url_image_test",
        "url" => "https://example.com/audio.mp3"
      })

      sounds = Repo.all(Sound)
      uploaded = Enum.find(sounds, &(&1.image_filename != nil))
      on_exit(fn -> if uploaded, do: ImageProcessing.delete_image(uploaded.image_filename) end)

      assert uploaded != nil, "Expected created sound to have an image_filename set"
      assert String.ends_with?(uploaded.image_filename, ".png")
    end
  end
end
